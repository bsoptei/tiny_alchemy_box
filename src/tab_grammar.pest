any_text = { ('a'..'z' | 'A'..'Z'| "\'" | "!" | "?" | "." | comma)+ }
bar = { bar_start ~ (notes | rest)+ ~ bar_end }
bar_end = { repeat_close | pipe }
bar_start = {  repeat_start | pipe }
bend = { "B1" | "B2" | "B3" | "B4" | "B5" }
comma = _{ "," }
diminutive = _{ "let" }
dot = { "." }
fret = _{ "F" | "fret" }
length = { "1L" | "2L" | "4L" | "8L"| "16L" | "32L" | "64L" }
link = { "->" }
note = { string ~ nums ~ ((fret ~ nums) | x) }
notes = { notes_token ~ (note)+ ~ comma ~ length ~ dot? ~ tuplet? ~ (comma ~ notes_modifier)? ~ link? }
notes_modifier = { "SL" | "PM" | "HM" | "~~" | bend }
notes_token = _{ "N" | "notes" }
number_of_strings_declaration = { "number of strings" ~ nums ~ semicolon }
num = { ASCII_DIGIT }
nums = { (num)+ }
parenthesis_open = _{ "(" }
parenthesis_close = _{ ")" }
pipe = { "|" }
repeat_close = { ":|" ~ nums? }
repeat_start = { "|:" }
rest = { rest_token ~ length ~ dot? ~ tuplet? ~ link? }
rest_token = _{ "R" | "rest" }
semicolon = _{ ";" }
slash = _{ "/" }
string = _{ "S" | "string" }
tab = {
    SOI ~
    title_declaration ~
    number_of_strings_declaration ~
    tuning_declaration ~
    tempo_declaration ~
    (time_signature? ~ bar)* ~
    EOI
}
tempo_declaration = { "tempo" ~ nums ~ semicolon }
time_signature = { time_token ~ nums ~ slash ~ length }
time_token = _{ "T" | "time" }
title_declaration = { "title" ~ any_text ~ semicolon }
tuning_declaration = { "tuning" ~ any_text ~ semicolon }
tuplet = { num ~ diminutive }
WHITESPACE = _{ " " | "\r\n" | "\n" }
x = { "X" }
